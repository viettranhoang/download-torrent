plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion 29
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.vit.demotorrent"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }


        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        /* Enable or disable logging in build types */
        def SESSION_LOGGING = "SESSION_LOGGING"

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField("boolean", SESSION_LOGGING, "false")
        }

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
            testCoverageEnabled true
            debuggable true

            buildConfigField("boolean", SESSION_LOGGING, "true")
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            universalApk true
        }
    }

    project.ext.versionCodes = ['armeabi-v7a': 1, 'arm64-v8a': 2, 'x86': 3, 'x86_64': 4]
    applicationVariants.all { variant ->
        outputs.all { output ->
            versionCodeOverride =
                    project.ext.versionCodes.get(output.getFilter(com.android.build.OutputFile.ABI), 0) * 1000000 + versionCode
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        dataBinding true
    }
}

dependencies {
    implementation project(':libretorrent')
    implementation project(':parser')

    implementation 'com.permissionx.guolindev:permissionx:1.4.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    /* libtorrent wrapper */
    def LIBTORRENT4J_VERSION = "1.2.3.0"
    implementation "org.libtorrent4j:libtorrent4j:$LIBTORRENT4J_VERSION"
    implementation "org.libtorrent4j:libtorrent4j-android-arm:$LIBTORRENT4J_VERSION"
    implementation "org.libtorrent4j:libtorrent4j-android-arm64:$LIBTORRENT4J_VERSION"
    implementation "org.libtorrent4j:libtorrent4j-android-x86:$LIBTORRENT4J_VERSION"
    implementation "org.libtorrent4j:libtorrent4j-android-x86_64:$LIBTORRENT4J_VERSION"

    /* ReactiveX */
    implementation "io.reactivex.rxjava2:rxjava:2.2.19"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"


    def ROOM_VERSION = "2.2.5"
    implementation "androidx.room:room-rxjava2:$ROOM_VERSION"
    implementation "androidx.room:room-runtime:$ROOM_VERSION"
    annotationProcessor "androidx.room:room-compiler:$ROOM_VERSION"

    implementation "org.apache.commons:commons-text:1.9"
    implementation "commons-io:commons-io:2.5"
    implementation "com.google.code.gson:gson:2.8.6"
    implementation "com.takisoft.preferencex:preferencex:1.1.0"
    implementation "com.leinardi.android:speed-dial:3.1.1"
    implementation "net.java.dev.jna:jna:5.6.0@aar"

    /* ACRA */
    def ACRA_VERSION = "5.7.0"
    implementation "ch.acra:acra-dialog:$ACRA_VERSION"
    implementation "ch.acra:acra-mail:$ACRA_VERSION"

    implementation "com.github.anthonynsimon:jurl:v0.4.2"

    implementation "androidx.work:work-runtime:2.4.0"
}